/*
 * Steganography utility to hide messages into cover files
 * Copyright (c) Samir Vaidya (mailto:syvaidya@gmail.com)
 */ 

import java.text.SimpleDateFormat 
import java.util.Date
import org.apache.tools.ant.filters.ReplaceTokens
import org.apache.tools.ant.filters.FixCrLfFilter

plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.langmo.gradlensis' version '0.1.0'
    id 'nebula.deb' version '8.4.1'
    id 'nebula.rpm' version '8.4.1'
}

def projectName = 'OpenStego'
def distBaseDir = file(project.buildDir.path + '/distributions/package')
def currYear = new SimpleDateFormat('yyyy').format(new Date())

version = project.version
description = projectName + ' is a tool implemented in Java for generic steganography, with support for password-based encryption of the data. Using ' \
                          + projectName + ', you can hide secret data within innocuous looking files like images.'

def filterTokens = [
    'app.name': projectName,
    'app.version': project.version,
    'author.name': project.author,
    'author.mail': project.authorMail,
    'homepage.url': project.homepageUrl,
    'time.year': currYear
]

repositories {
    jcenter()
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            exclude 'com/openstego/desktop/plugin/dctlsb/**'
            exclude 'com/openstego/desktop/plugin/dwtkim/**'
            exclude 'com/openstego/desktop/plugin/dwtxie/**'
            exclude 'com/openstego/desktop/plugin/template/dct/**'
            exclude 'com/openstego/desktop/util/dct/**'
       }
       resources {
           exclude 'com/openstego/desktop/resource/i18n/DctLSB*'
           exclude 'com/openstego/desktop/resource/i18n/DCTPlugin*'
           exclude 'com/openstego/desktop/resource/i18n/DWTKim*'
           exclude 'com/openstego/desktop/resource/i18n/DWTXie*'
           exclude 'com/openstego/desktop/resource/i18n/*_ja.properties'
       }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
}

compileJava {
    options.release = 8
}

processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens: filterTokens)
    }
}

jar {
    archiveFileName = 'openstego.jar'
    manifest {
        attributes([
            'Name': projectName,
            'Main-Class': 'com.openstego.desktop.OpenStego',
            'Implementation-Version': project.version,
            'Built-By': project.author
        ])
    }
}

task run(type: JavaExec, dependsOn: jar) {
    classpath = files(tasks.jar)
}

test {
    useJUnit()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
    }
}

javadoc {
    title projectName + ' v' + project.version
    exclude 'com/openstego/desktop/plugin/**'
    options.bottom 'Copyright &copy; 2007-' + currYear + '  ' + project.author + '. All Rights Reserved.'
    options.links project.jdkDocsUrl
    options.addStringOption('Xdoclint:none', '-quiet')
}

task distBase(type: Copy, dependsOn: jar) {
    destinationDir = distBaseDir
    from('.') {
        include 'LICENSE'
    }
    from('src/main/dist') {
        exclude '**/*.sh'
        filesNotMatching('**/*.ico') {
            filter(ReplaceTokens, tokens: filterTokens)
        }
    }
    from('src/main/dist') {
        include '**/*.sh'
        filter(ReplaceTokens, tokens: filterTokens)
        fileMode 0755
    }
    from(project.buildDir.path + '/' + project.libsDirName) {
        into 'lib'
    }
}

task distBin(type: Zip, dependsOn: distBase) {
    from distBaseDir
    into project.name + '-' + project.version
}

nsis {
    configuration = file('artifacts/installer.nsi')
    runIn =  file('.')
    variables = [
        'AppName': projectName,
        'AppVersion': project.version,
        'AppDir': distBaseDir.path,
        'DistDir': project.buildDir.path + '\\distributions'
    ]
}

createInstaller.dependsOn distBase

task distDeb(type: Deb, dependsOn: distBase) {
    packageName = project.name
    summary = projectName
    release = 1
    packageGroup = 'utils'
    maintainer = project.author + ' (' + project.authorMail + ')'
    url = project.homepageUrl
    requires('default-jre').or('java8-runtime')

    into '/usr/share'
    from (distBaseDir) {
        into project.name
        include 'lib/**'
    }
    from (distBaseDir) {
        into project.name
        include 'openstego.sh'
        rename('openstego.sh', 'openstego')
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance('lf'))
        fileMode 0755
    }
    from ('artifacts') {
        into 'applications'
        include 'openstego.desktop'
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance('lf'))
        filter(ReplaceTokens, tokens: filterTokens)
    }
    from ('artifacts') {
        into 'icons/hicolor/scalable/apps'
        include 'openstego.svg'
    }
    from (distBaseDir) {
        into 'doc/' + project.name
        include 'README'
    }
    from ('artifacts') {
        into 'doc/' + project.name
        include 'copyright'
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance('lf'))
        filter(ReplaceTokens, tokens: filterTokens)
    }

    link('/usr/bin/openstego', '/usr/share/openstego/openstego')
}

task distRpm(type: Rpm, dependsOn: distBase) {
    packageName = project.name
    summary = projectName
    release = 1
    packageGroup = 'utils'
    packager = project.author + ' (' + project.authorMail + ')'
    url = project.homepageUrl
    user = 'root'
    license = 'GPLv2'
    buildHost = ''
    os = LINUX
    requires('java', '1.8', GREATER | EQUAL)

    into '/usr/share'
    from (distBaseDir) {
        into project.name
        include 'lib/**'
    }
    from (distBaseDir) {
        into project.name
        include 'openstego.sh'
        rename('openstego.sh', 'openstego')
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance('lf'))
        fileMode 0755
    }
    from ('artifacts') {
        into 'applications'
        addParentDirs false 
        include 'openstego.desktop'
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance('lf'))
        filter(ReplaceTokens, tokens: filterTokens)
    }
    from ('artifacts') {
        into 'icons/hicolor/scalable/apps'
        addParentDirs false 
        include 'openstego.svg'
    }
    from (distBaseDir) {
        into 'doc/' + project.name
        addParentDirs false 
        include 'LICENSE'
        include 'README'
        filter(FixCrLfFilter, eol:FixCrLfFilter.CrLf.newInstance('lf'))
    }

    link('/usr/bin/openstego', '/usr/share/openstego/openstego')
}

task dist(dependsOn: [distBin, createInstaller, distDeb, distRpm]) {
}
